# @.arg.{f: Callable, args: List<Object>, suc: Pid}
proc py_call {
    %arg = @.arg;

    %ty = @.arg.f.type;
    %typemap = {};
    %typemap[%ty] = py_call_other;
    %typemap[@.singletons.function] = py_call_fn;
    %typemap[@.singletons.type] = py_call_class;
    jmp %typemap[%ty];
}

proc py_call_fn {
    %arg = @.arg;

    @.arg = {};
    @.arg.f = %arg.f.payload;
    @.arg.farg = %arg.args;
    @.arg.suc = %arg.suc;
    jmp call_fn;
}

proc py_call_class {
    @.frame.irlocals.py_call_arg = @.arg;

    @.arg = {};
    @.arg.ty = @.frame.irlocals.py_call_arg.f;
    @.arg.attr = "__init__";
    @.arg.suc = py_call_class_1;
    jmp py_type_attrlookup;
}

proc py_call_class_1 {
    # %constr :: FunctionObject | Undef
    %constr = @.ret;

    @.frame.irlocals.obj = {};
    @.frame.irlocals.obj.type = @.frame.irlocals.py_call_arg.f;
    @.frame.irlocals.obj.dict = {};

    %tab = {};
    %tab[%constr] = py_call_class_w_constructor;
    %tab[Undef] = py_call_class_done;
    jmp %tab[%constr];
}

proc py_call_class_w_constructor {
    @.frame.irlocals.py_call_f = @.ret.payload;

    @.arg = {};
    @.arg.self = @.frame.irlocals.obj;
    @.arg.args = @.frame.irlocals.py_call_arg.args;
    @.arg.suc = py_call_class_w_constructor_2;

    jmp self_prefix_args;
}

proc py_call_class_w_constructor_2 {
    %prefixed_args = @.ret;

    @.arg = {};
    @.arg.f = @.frame.irlocals.py_call_f;
    @.arg.farg = %prefixed_args;
    @.arg.suc = py_call_class_done;
    jmp call_fn;
}

proc py_call_class_done {
    @.ret = @.frame.irlocals.obj;
    jmp @.frame.irlocals.py_call_arg.suc;
}

proc py_call_other {
    panic "todo!";
}


##########

# @.arg.{self: Object, args: List<Object>, suc: ProcId}
# @.ret = [self, ..args]
proc self_prefix_args {
    @.ret = {};
    @.ret[0] = @.arg.self;
    @.frame.irlocals.prefix_i = 0;

    jmp self_prefix_args_loop;
}

proc self_prefix_args_loop {
    %v = @.arg.args[@.frame.irlocals.prefix_i];
    @.ret[@.frame.irlocals.prefix_i + 1] = %v;
    @.frame.irlocals.prefix_i = @.frame.irlocals.prefix_i + 1;

    %tab = {};
    %tab[%v] = self_prefix_args_loop;
    %tab[Undef] = @.arg.suc;
    jmp %tab[%v];
}
