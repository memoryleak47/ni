#
main proc init {
    # setup singletons
    @.singletons = {};
        @.singletons.function = {}; # the `function` type.


        # the `int` type.
        %int = {};
            %int.dict = {};

            %add_fn = {};
            %add_fn.type = @.singletons.function;
            %add_fn.payload = payload_add;
            %int.dict["__add__"] = %add_fn;

            %eq_fn = {};
            %eq_fn.type = @.singletons.function;
            %eq_fn.payload = payload_eq;
            %int.dict["__eq__"] = %eq_fn;
        @.singletons.int = %int;

        @.singletons.str = {};      # the `str` type.
        @.singletons.bool = {};     # the `bool` type.
        @.singletons.none = {};     # the unique `None` value.

    # setup globals
    @.globals = {};

    # setup print
    %print_fn = {};
    %print_fn.type = @.singletons.function;
    %print_fn.payload = print_fn;
    @.globals["print"] = %print_fn;

    # setup callstack
    %frame = {};
    %frame.parent = undef;
    %frame.retpid = fin;
    %frame.pylocals = @.globals;
    %frame.irlocals = {};
    @.frame = %frame;

    jmp userstart;
}

proc print_fn {
    print @.arg[0].payload;
    @.ret = @.singletons.none;
    jmp pop_stack;
}

# @.arg{0, 1}
proc payload_add {
    @.ret = {};
    @.ret.payload = @.arg[0].payload + @.arg[1].payload;
    @.ret.type = @.arg[0].type;
    jmp pop_stack;
}

# @.arg{0, 1}
proc payload_eq {
    @.ret = {};
    @.ret.payload = @.arg[0].payload == @.arg[1].payload;
    @.ret.type = @.singletons.bool;
    jmp pop_stack;
}

proc fin {
    exit;
}

proc pop_stack {
    %retpid = @.frame.retpid;
    @.frame = @.frame.parent;
    jmp %retpid;
}

# @.arg.{f, suc, farg}
proc call_fn {
    %arg = @.arg;

    %frame = {};
    %frame.parent = @.frame;
    %frame.retpid = %arg.suc;
    %frame.pylocals = {};
    %frame.irlocals = {};

    @.frame = %frame;

    @.arg = %arg.farg;
    jmp %arg.f;
}
